@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Tender: Mass Change'
@Metadata.allowExtensions: true

define root view entity ZL2C_I_TENDER_MASS_CHANGE
  as select from ZL2C_I_TENDER_m_DATASELECT_TF(client : $session.client) as selector
    inner join   I_SalesContract                                         as SalesContract on SalesContract.SalesContract = selector.SalesContract

  association [1..1] to v_konv                   as konv          on  konv.knumv = SalesContract.SalesContractCondition
                                                                  and konv.kposn = selector.SalesContractItem
                                                                  and konv.kschl = 'YTPR'

  association [1..1] to I_SalesContractItem      as Item          on  Item.SalesContract     = selector.SalesContract
                                                                  and Item.SalesContractItem = selector.SalesContractItem

  association [1..1] to vbap                     as _vbap         on  _vbap.vbeln = $projection.SalesContract
                                                                  and _vbap.posnr = $projection.SalesContractItem

  association [1..1] to ZL2C_I_TENDER_DATA       as _TenderData   on  _TenderData.SalesContract = $projection.SalesContract

  association [0..1] to zl2c_tendr_m_msg         as TenderMessage on  TenderMessage.vbeln = $projection.SalesContract
                                                                  and TenderMessage.posnr = $projection.SalesContractItem
                                                                  and TenderMessage.uname = $session.user

  association [1..1] to ZL2C_I_TENDER_ZSTATUS_VH as _TenderStatus on  _TenderStatus.Status = $projection.TenderStatus

{
      @Consumption.semanticObject: 'SalesContract'
  key cast(selector.SalesContract as sales_contract preserving type)       as SalesContract,
  key selector.SalesContractItem                                           as SalesContractItem,
      @ObjectModel.foreignKey.association: '_SalesOrganization'
      SalesContract.SalesOrganization                                      as SalesOrganization,
      SalesContract._SalesOrganization._Text[1:Language=$session.system_language].SalesOrganizationName,
      @ObjectModel.foreignKey.association: '_DistributionChannel'
      SalesContract.DistributionChannel                                    as DistributionChannel,
      SalesContract._DistributionChannel._Text[1:Language=$session.system_language].DistributionChannelName,
      @ObjectModel.foreignKey.association: '_OrganizationDivision'
      SalesContract.OrganizationDivision                                   as OrganizationDivision,
      SalesContract.SalesContractValidityStartDate                         as ValidityStartDate,
      SalesContract.SalesContractValidityEndDate                           as ValidityEndDate,
      Item.PricingDate                                                     as PricingDate,
      SalesContract.TransactionCurrency                                    as Currency,
      _TenderData.TenderStatus                                             as TenderStatus,
      Item.Material                                                        as Material,
      Item._MaterialText[1:Language=$session.system_language].MaterialName as MaterialText,
      @Semantics.quantity.unitOfMeasure: 'OrderQuantityUnit'
      Item.TargetQuantity                                                  as OrderQuantity,
      Item.TargetQuantityUnit                                              as OrderQuantityUnit,
      Item.SalesDocumentRjcnReason                                         as RejectionReason,
      @Semantics.amount.currencyCode: 'Currency'
      konv.kbetr                                                           as ConditionAmount,
      konv.kpein                                                           as ConditionQuantity,
      konv.kmein                                                           as ConditionUoM,
      _vbap.zzbismt                                                        as BaseMaterial,
      TenderMessage.msgtxt                                                 as Message,
      TenderMessage.msgty                                                  as MessageType,
      TenderMessage.crit                                                   as CriticalityType,
      SalesContract._SalesOrganization,
      SalesContract._DistributionChannel,
      SalesContract._OrganizationDivision,
      _TenderStatus,
      SalesContract.PurchaseOrderByCustomer                                as PurchaseOrderByCustomer

}
